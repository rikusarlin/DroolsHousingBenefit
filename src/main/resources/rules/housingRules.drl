//created on: 20.3.2019
package fi.kela.drools.housing

import java.math.BigDecimal;

import fi.kela.drools.general.Asiakas;
import fi.kela.drools.housing.YlatEtuusAsia;
import fi.kela.drools.general.PaatoksenFraasi;
import fi.kela.drools.housing.RuokakunnanJasen;
import fi.kela.drools.housing.YlatPerusteTiedot;
import fi.kela.drools.housing.AsuntoTiedot;
import fi.kela.drools.housing.AsunnonTyyppi;
import fi.kela.drools.housing.YlatTiedot;

rule "Ruokakunnan koko"
	ruleflow-group "ruokakunnanKoko"
	no-loop true
    when
       $per:YlatPerusteTiedot(ruokakunnanJasenMaara > 0)
       $rat:YlatRatkaisuTiedot()
       $asiakas:Asiakas()
       $asia:YlatEtuusAsia()
    then
       $per.setRuokakunnanKoko($per.getRuokakunnanJasenMaara());
       $asia.lisaaFraasi("AY002");
       update($asia)
end

rule "Ruokakunnan koko, vammaiskorotus"
	ruleflow-group "vammaiskorotus"
	no-loop true
    when
        $per:YlatPerusteTiedot(!lisatilanTarveHuomioitu)
  		RuokakunnanJasen(lisatilanTarve)
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
        $asia:YlatEtuusAsia()
    then
        // Päivitetään vain kerran, vaikka olisi useampi kuin yksi jolla olisi lisätilan tarvetta
        // Tätä kontrolloidaan PerusteTietojen "lisatilanTarveHuomioitu" -lipulla - lasketaan vain kerran
	    int uusiRuokakunnanKoko = $per.getRuokakunnanKoko() + 1;
        $per.setRuokakunnanKoko(uusiRuokakunnanKoko);
        $per.setLisatilanTarveHuomioitu(true);
        $asia.lisaaFraasi("AY001");
        update($per)
        update($asia)
end

rule "Asunnon hyväksytyt hoitomenot, lämmitys"
	// YLAT 9§
    //Vuokra-asunnossa asuvan ruokakunnan asumismenoiksi hyväksytään vuokra ja erikseen maksettavat vesimaksut ja lämmityskustannukset.
    //Erikseen maksettavina lämmityskustannuksina 38 euroa kuukaudessa yhden hengen ruokakunnassa.
    //Jos ruokakuntaan kuuluu useampi kuin yksi henkilö, lämmityskustannuksiin lisätään 13 euroa jokaista lisähenkilöä kohden.
    //
	lock-on-active true
	ruleflow-group "hoitomenot"
    when
        $asunto:AsuntoTiedot(vuokraAsunto || omistusOsake, lammitysKustannuksia)
        $asia: YlatEtuusAsia()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
        $per:YlatPerusteTiedot()
    then
       BigDecimal lammitysKustannukset = new BigDecimal(0);
       BigDecimal lammitysPohja = new BigDecimal(0);
       BigDecimal rajaArvo = $per.haeOhjausTieto(YlatTiedot.MAX_LAMMITYS_KK);
       BigDecimal lisaPerHlo = $per.haeOhjausTieto(YlatTiedot.LAMMITYS_LISAHLO_KK);
       int ruokakunnanJasenet = $per.getRuokakunnanJasenet().size();
       if($asunto.getLammitysKustannuksetEur().compareTo(rajaArvo) > 0){
           lammitysPohja = rajaArvo;
       } else {
           lammitysPohja = $asunto.getLammitysKustannuksetEur();
       }
       if(ruokakunnanJasenet == 1){
           lammitysKustannukset = lammitysPohja;
       } else if (ruokakunnanJasenet > 1){
           lammitysKustannukset = lammitysPohja.add((new BigDecimal(ruokakunnanJasenet-1)).multiply(lisaPerHlo)); 
       }
       $rat.setLammitysMeno(lammitysKustannukset);
       $asia.lisaaFraasi("AY004");
       update($asia);
       update($rat);
end

rule "Asunnon hyväksytyt hoitomenot, vesi"
	// YLAT 9§ mom 1
    //Vuokra-asunnossa asuvan ruokakunnan asumismenoiksi hyväksytään vuokra ja erikseen maksettavat vesimaksut ja lämmityskustannukset.
    //Erikseen maksettavina vesimaksuina otetaan huomioon 17 euroa henkilöä kohden kuukaudessa.
    //
    // Osakeasunnon hoitomenoiksi hyväksytään vastike sekä erikseen maksettavat vesimaksut ja lämmityskustannukset.
    // Vesimaksut ja lämmityskustannukset otetaan huomioon 1 momentin mukaisina.
	lock-on-active true
	ruleflow-group "hoitomenot"
    when
        $asunto:AsuntoTiedot(vuokraAsunto || omistusOsake, vesiKustannuksia)
        $asia: YlatEtuusAsia()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
        $per:YlatPerusteTiedot()
    then
       BigDecimal vesimaksu = new BigDecimal(0);
       BigDecimal hyvaksyttyVesimaksuPerHlo = new BigDecimal(0);
       BigDecimal rajaArvo = $per.haeOhjausTieto(YlatTiedot.MAX_VESIMAKSU_HLO_KK);
       if($asunto.getVesiPerHenkiloEur().compareTo(rajaArvo) > 0) {
       	   hyvaksyttyVesimaksuPerHlo = rajaArvo;
       } else {
       	   hyvaksyttyVesimaksuPerHlo = $asunto.getVesiPerHenkiloEur();
       }
       vesimaksu = hyvaksyttyVesimaksuPerHlo.multiply(new BigDecimal($asia.getPerusteTiedot().getRuokakunnanJasenet().size()));
       $rat.setVesiMeno(vesimaksu);
       $asia.lisaaFraasi("AY007");
       update($asia);
end

rule "Vuokra-asunnon hyväksytyt hoitomenot, vuokra"
    //Vuokra-asunnossa asuvan ruokakunnan asumismenoiksi hyväksytään vuokra ja erikseen maksettavat vesimaksut ja lämmityskustannukset.
    //
	lock-on-active true
	ruleflow-group "hoitomenot"
    when
        $asunto:AsuntoTiedot(vuokraAsunto, vuokraa)
        $asia: YlatEtuusAsia()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
    then
        $rat.setVuokraMeno($asunto.getVuokraEur());
        $asia.lisaaFraasi("AY008");
        update($asia);
end

rule "Omistusosakkeen hyväksytyt hoitomenot"
    // Omistusasunnossa asuvan ruokakunnan asumismenoiksi hyväksytään asunnon hoito- ja rahoitusmenot.
    // Osakeasunnon hoitomenoiksi hyväksytään vastike sekä erikseen maksettavat vesimaksut ja lämmityskustannukset.
    // Vesimaksut ja lämmityskustannukset otetaan huomioon 1 momentin mukaisina.
	ruleflow-group "hoitomenot"
	lock-on-active true
    when
        $asunto:AsuntoTiedot(omistusOsake, vastiketta)
        $asia: YlatEtuusAsia()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
    then
        $rat.setVastikeMeno($asunto.getVastikeEur());
        $asia.lisaaFraasi("AY009");
        update($asia);
end

rule "Muun omistusasunnon hyväksytyt hoitomenot"
    //Muun omistusasunnon hoitomenoiksi hyväksytään:
    //Ruokakunnan koko henkeä 	Huomioon otettava hoitomeno €/kk
    //1 	89
    //2 	107
    //3 	135
    //4 	159
    //Jos ruokakuntaan kuuluu enemmän kuin neljä henkilöä, muun omistusasunnon hoitomenoja korotetaan 49 euroa jokaista neljä henkilöä ylittävää ruokakunnan jäsentä kohden.
	ruleflow-group "hoitomenot"
	lock-on-active true
    when
        $asunto:AsuntoTiedot(muuOmistus)
        $per:YlatPerusteTiedot(ruokakunnanJasenMaara > 0)
        $asia: YlatEtuusAsia()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
    then
       BigDecimal hoitoMenot = new BigDecimal(0);
       int ruokakunnanJasenMaara = $per.getRuokakunnanJasenet().size();
       switch (ruokakunnanJasenMaara){
           case 1: hoitoMenot = $per.haeOhjausTieto(YlatTiedot.HOITOMENO_1HLO); break;
           case 2: hoitoMenot = $per.haeOhjausTieto(YlatTiedot.HOITOMENO_2HLO); break;
           case 3: hoitoMenot = $per.haeOhjausTieto(YlatTiedot.HOITOMENO_3HLO); break;
           case 4: hoitoMenot = $per.haeOhjausTieto(YlatTiedot.HOITOMENO_4HLO); break;
           default:
               BigDecimal hoitoMeno4 = $per.haeOhjausTieto(YlatTiedot.HOITOMENO_4HLO);
               BigDecimal hoitoMenoYli4 = $per.haeOhjausTieto(YlatTiedot.HOITOMENO_YLI4HLO);
               hoitoMenot = hoitoMeno4.add((new BigDecimal(ruokakunnanJasenMaara-4)).multiply(hoitoMenoYli4)); 
               break;
       }
       $rat.setHoitoMenoMuu(hoitoMenot);
       $asia.lisaaFraasi("AY006");
       update($asia);
       update($rat);
end

rule "Omistusasunnon hyväksytyt rahoitusmenot"
    // Omistusasunnossa asuvan ruokakunnan rahoitusmenoiksi hyväksytään asunnon hankkimiseksi ja perusparantamiseksi otettujen henkilökohtaisten lainojen 
    // kuukausittaisista koroista 73 prosenttia. Asumisoikeusasunnon menoiksi hyväksytään 73 prosenttia asumisoikeuden hankkimiseksi otettujen henkilökohtaisten 
    // lainojen kuukausittaisista koroista. Edellä mainittujen lainojen on oltava valtion, kunnan, seurakunnan tai julkisen valvonnan alaisen luottotoimintaa 
    // harjoittavan laitoksen myöntämiä.
	ruleflow-group "hoitomenot"
	no-loop true
    when
        $asunto:AsuntoTiedot(omistusOsake || muuOmistus, rahoitusMenoja)
        $per:YlatPerusteTiedot()
        $asia: YlatEtuusAsia()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
    then
       $rat.setRahoitusMeno($asunto.getRahoitusMenotEur().multiply($per.haeOhjausTieto(YlatTiedot.RAHOITUSMENOT_KORVATTAVA_OSUUS)));
       $asia.lisaaFraasi("AY010");
       update($asia);
end

rule "Hyväksytyt hoito- ja lämmitysmenot, korotus maakunnan mukaan, Keski-Suomi"
    // Edellä 1 ja 2 momentissa mainittuja lämmityskustannuksia sekä 3 ja 4 momentissa mainittuja hoitomenoja korotetaan neljä prosenttia
    // Etelä-Savon, Pohjois-Savon ja Pohjois-Karjalan maakunnissa ja kahdeksan prosenttia Pohjois-Pohjanmaan, Kainuun ja Lapin maakunnissa.
	ruleflow-group "hoitomenot-korotukset"
	salience -5
	lock-on-active true
    when
        $per:YlatPerusteTiedot(asuinMaakuntaItaSuomessa)
        $rat:YlatRatkaisuTiedot(hoitoMenoja || lammitysMenoja)
        $asia:YlatEtuusAsia()
        $asunto:AsuntoTiedot()
    then
       BigDecimal korotus = $per.haeOhjausTieto(YlatTiedot.KUSTANNUS_KOROTUS_ITA);
       $rat.setLammitysMeno($rat.getLammitysMeno().multiply(new BigDecimal(1.0).add(korotus))); 
       $rat.setHoitoMenoMuu($rat.getHoitoMenoMuu().multiply(new BigDecimal(1.0).add(korotus)));
       $asia.lisaaFraasi("AY005"); 
       update($asia);
       update($rat);
end

rule "Hyväksytyt hoito- ja lämmitysmenot, korotus maakunnan mukaan, Pohjois-Suomi"
    // Edellä 1 ja 2 momentissa mainittuja lämmityskustannuksia sekä 3 ja 4 momentissa mainittuja hoitomenoja korotetaan neljä prosenttia
    // Etelä-Savon, Pohjois-Savon ja Pohjois-Karjalan maakunnissa ja kahdeksan prosenttia Pohjois-Pohjanmaan, Kainuun ja Lapin maakunnissa.
	ruleflow-group "hoitomenot-korotukset"
	salience -5
	lock-on-active true
    when
        $per:YlatPerusteTiedot(asuinMaakuntaPohjoisSuomessa)
        $rat:YlatRatkaisuTiedot(hoitoMenoja || lammitysMenoja)
        $asia:YlatEtuusAsia()
        $asunto:AsuntoTiedot()
    then
       BigDecimal korotus = $per.haeOhjausTieto(YlatTiedot.KUSTANNUS_KOROTUS_POHJOINEN);
       $rat.setLammitysMeno($rat.getLammitysMeno().multiply(new BigDecimal(1.0).add(korotus))); 
       $rat.setHoitoMenoMuu($rat.getHoitoMenoMuu().multiply(new BigDecimal(1.0).add(korotus)));
       $asia.lisaaFraasi("AY005");  
       update($asia);
       update($rat);
end

rule "Vuokra-asunnon hoitomenot, alivuokralainen"
    // Jos vuokralla asuvalla ruokakunnalla on alivuokralainen, vähennetään ruokakunnan tässä pykälässä tarkoitetuista asumismenoista 
    // alivuokralaisen maksaman vuokran määrä.
	ruleflow-group "hoitomenot"
	salience -5
	lock-on-active true
    when
        $asunto:AsuntoTiedot(vuokraAsunto, tulojaAlivuokralaisesta)
        $asia: YlatEtuusAsia()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
        $per:YlatPerusteTiedot()
    then
        BigDecimal alivuokra = $asunto.getAlivuokralaisenMaksamaVuokraEur();
        BigDecimal vuokraMeno = $rat.getVuokraMeno();
        BigDecimal uusiVuokraMeno = vuokraMeno.subtract(alivuokra);
        // Kaipa tämäkin on mahdollista - ei mennä negatiivisen puolelle?
        if(vuokraMeno.compareTo(alivuokra) < 0){
        	uusiVuokraMeno = new BigDecimal(0);
        }
        $rat.setVuokraMeno(uusiVuokraMeno);
        $asia.lisaaFraasi("AY014");
        update($asia);
        update($rat);
end

rule "Osakeasunnon hyväksytyt hoito- ja rahoitusmenot, rajaus suhteessa hyväksyttyihin enimmäisasumismenoihin"
    // Osakeasunnossa asuvan ruokakunnan 2 momentissa tarkoitetuista asunnon hoitomenoista otetaan huomioon enimmäishoitomenoina enintään määrä, 
    // joka on 30 prosenttia 10 §:ssä tarkoitetuista ruokakunnan enimmäisasumismenoista ja enimmäisrahoitusmenoina enintään määrä, joka on 70 prosenttia 
    // ruokakunnan enimmäisasumismenoista.
    // Jos hoitomenot ylittävät enimmäishoitomenot, ylimenevästä osasta otetaan huomioon 73 prosenttia asunnon hyväksyttävinä rahoitusmenoina.
	ruleflow-group "hoitomeno-rajaus"
	lock-on-active true
    when        
        $asunto:AsuntoTiedot(omistusOsake, vastiketta|| lammitysKustannuksia || vesiKustannuksia || rahoitusMenoja)
        $per:YlatPerusteTiedot()
        $rat:YlatRatkaisuTiedot()
        $asiakas:Asiakas()
        $asia:YlatEtuusAsia()
    then
       BigDecimal maxHoitoMenotOsuus = $per.haeOhjausTieto(YlatTiedot.MAX_HOITOMENOT_OSUUS);
       BigDecimal maxRahoitusMenotOsuus = $per.haeOhjausTieto(YlatTiedot.MAX_RAHOITUSMENOT_OSUUS);
       BigDecimal enimmaisHoitoMeno = maxHoitoMenotOsuus.multiply($asia.getRatkaisuTiedot().getEnimmaisAsumisMenotEur());
       BigDecimal enimmaisRahoitusMeno = maxRahoitusMenotOsuus.multiply($asia.getRatkaisuTiedot().getEnimmaisAsumisMenotEur());
       BigDecimal korvattavaOsuus = $per.haeOhjausTieto(YlatTiedot.RAHOITUSMENOT_KORVATTAVA_OSUUS);
       BigDecimal lisaRahoitusMeno = new BigDecimal(0);
       if($asia.getRatkaisuTiedot().getHyvaksytytHoitomenotOsakeEur().compareTo(enimmaisHoitoMeno) > 0){
           lisaRahoitusMeno = $rat.getHyvaksytytHoitomenotOsakeEur().subtract(enimmaisHoitoMeno).multiply(korvattavaOsuus);
           // Korjataan vastiketta (voisi korjata myös lämmitys- tai vesikustannuksia, mutta tämä selkeämpää?
           $rat.setVastikeMeno(enimmaisHoitoMeno.subtract($asia.getRatkaisuTiedot().getVesiMeno()).subtract($asia.getRatkaisuTiedot().getLammitysMeno()));
           $rat.getFraasit().add(new PaatoksenFraasi("AY011", $asiakas.getKieli(), 
               (maxHoitoMenotOsuus.multiply(new BigDecimal(100))).setScale(0,BigDecimal.ROUND_HALF_EVEN).toString(),
               $rat.getEnimmaisAsumisMenotEur().setScale(2,BigDecimal.ROUND_HALF_EVEN).toString(),
               enimmaisHoitoMeno.setScale(2,BigDecimal.ROUND_HALF_EVEN).toString()));
       }
       if($rat.getRahoitusMeno().add(lisaRahoitusMeno).compareTo(enimmaisRahoitusMeno) > 0){
           $rat.setRahoitusMeno(enimmaisRahoitusMeno);
           $rat.getFraasit().add(new PaatoksenFraasi("AY012", $asiakas.getKieli(), 
               (maxRahoitusMenotOsuus.multiply(new BigDecimal(100))).setScale(0,BigDecimal.ROUND_HALF_EVEN).toString(),
               $asia.getRatkaisuTiedot().getEnimmaisAsumisMenotEur().setScale(2,BigDecimal.ROUND_HALF_EVEN).toString(),
               enimmaisRahoitusMeno.setScale(2,BigDecimal.ROUND_HALF_EVEN).toString()));
       } else if (lisaRahoitusMeno.compareTo(new BigDecimal(0)) > 0){
           $rat.getFraasit().add(new PaatoksenFraasi("AY013", $asiakas.getKieli(),
               (korvattavaOsuus.multiply(new BigDecimal(100))).setScale(0,BigDecimal.ROUND_HALF_EVEN).toString(),
               $rat.getRahoitusMeno().setScale(2,BigDecimal.ROUND_HALF_EVEN).toString(), 
               $rat.getRahoitusMeno().add(lisaRahoitusMeno).setScale(2,BigDecimal.ROUND_HALF_EVEN).toString()));
           $rat.setRahoitusMeno($asia.getRatkaisuTiedot().getRahoitusMeno().add(lisaRahoitusMeno));
       }
       // Jos päivitetään vain $asia, useampia muita aikaisempia sääntöjä käynnistyy uudelleen
       // Tätä ei tapahdu muiden sääntöjen kanssa!
       update($rat);
end

rule "Omavastuuosuus"
	ruleflow-group "omavastuu"
	lock-on-active true
    when
       $asia: YlatEtuusAsia()
    then
end

rule "Asumismenot"
	ruleflow-group "asumismenot"
	lock-on-active true
    when
       $asia: YlatEtuusAsia()
    then
end

